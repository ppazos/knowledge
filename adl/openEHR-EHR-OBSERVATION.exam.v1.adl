archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.exam.v1

concept
	[at0000]	-- Physical examination findings
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["es"] = <
			language = <[ISO_639-1::es]>
			author = <
				["name"] = <"Pablo Pazos">
				["organisation"] = <"CaboLabs">
			>
			accreditation = <"Computer Engineer">
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2007-11-10">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording details about findings on physical examination of the subject of care.">
			use = <"Use to record details about findings on physical examination of the subject of care. This may include a narrative description of the findings, a framework in which to nest detailed CLUSTER examination archetypes, and a clinical interpretation of the findings.

Examples of detailed CLUSTER examination archetypes include those that describe inspection, palpation, auscultation, percussion and movement of body systems or anatomical structures, such as CLUSTER.exam_pupils.

Narrative descriptions of clinical findings from existing clinical systems may be captured using the 'Description' data element.

Clinicians may sometimes want to record a summative phrase such as 'chronic otitis media' as an Interpretation of the physical examination. In the context of the physical examination archetype this should only be understood  as 'physical signs are consistent with chronic otitis media'. While the summative phrase may appear to represent a diagnosis, safe and consistent querying requires a diagnosis to be recorded using the EVALUATION.problem_diagnosis archetype, even if the phrases are identical.">
			keywords = <"examination", "physical", "exam", "findings", "clinical">
			misuse = <"Not to be used for recording history-taking observations - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure, OBSERVATION.body_weight, or OBSERVATION.height.">
			copyright = <"© openEHR Foundation">
		>
		["es"] = <
			language = <[ISO_639-1::es]>
			purpose = <"Este arquetipo será utilizado para registrar detalles sobre los hallazgos en el examen físico del paciente">
			use = <"Este arquetipo será utilizado para registrar detalles sobre los hallazgos en el examen físico del paciente.

Puede utilizarse junto a otros arquetipos para incluir detalles de los hallazgos clínicos, para describir examinación por inspección, palpación, auscultación, percusión y movimiento de los sistemas o estructuras anatómicas. Por ejemplo se podrá utilizar el arquetipo CLUSTER.exam_pupils.

La información registrada mediante este arquetipo debe estar relacionada con la examinación física y sus hallazgos. Algunas interpretaciones de los hallazgos pueden registrarse usando este arquetipo aunque parezcan diagnósticos, pero solo si están directamente relacionadas con el hallazgo. Para registrar un diagnóstico utilizar el arquetipo EVALUATION.problem_diagnosis, aunque el texto de la interpretación del hallazgo sea igual al diagnóstico.">
			keywords = <"examinación", "palpación", "auscultación", "percusión", "hallazgos clínicos", "examen físico">
			misuse = <"No se debe utilizar para registrar información histórica del paciente, para eso utilizar el arquetipo OBSERVATION.story

No se debe utilizar para registrar observaciones independientes y signos, para eso utilizar arquetipos específicos como OBSERVATION.blood_pressure, OBSERVATION.body_weight, OBSERVATION.height, etc.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"Published">
	other_contributors = <"Sheryl Alexander, NT Department of Health, Australia", "Tomas Alme, DIPS, Norway", "Nadim Anani, Karolinska Institutet, Sweden", "Rita Apelt, Department of Health,NT, Australia", "Silje Bakke, Bergen Hospital Trust, Norway", "Margaret Campbell, Queensland Health, Australia", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, NeHTA, Australia", "Margaret Cotter, AMSANT, Australia", "Michelle Dowden, Miwatj Health Ngalkanbuy Health, Australia", "Samo Drnovsek, Marand ltd, Slovenia", "Tim Garden, NTG Department of Health, Australia", "Tanya Gardner, CAAC, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Lars Karlsen, DIPS ASA, Norway", "Russell Leftwich, Russell B Leftwich MD, United States", "Sabine Leh, Haukeland University Hospital, Department of Pathology, Norway", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (Editor)", "Jeremy Oats, NT Health, Australia", "Steven Schatz, Department of Health (Northern Territory), Australia", "Anoop Shah, University College London, United Kingdom", "Cherie Whitbread, Royal Darwin Hospital, Australia", "Jo Wright, NT Dept of Health, Australia (Editor)", "Lin Zhang, BIPH, China">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["MD5-CAM-1.0.1"] = <"E240B3D98346DC3F227C9316EF8CEAC7">
	>

definition
	OBSERVATION[at0000] matches {	-- Physical examination findings
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Examination detail
										include
											archetype_id/value matches {/.*/}
									}
									ELEMENT[at0006] occurrences matches {0..*} matches {	-- Interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0011] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0009] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0008] occurrences matches {0..*} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0007] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0010] occurrences matches {0..*} matches {	-- Device details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Physical examination findings">
					description = <"Findings observed during the physical examination of a subject of care.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Description">
					description = <"Narrative description of the overall findings observed during a physical examination of a patient.">
					comment = <"May be used to record a narrative summary of the complete clinical examination or key aspects of clinical examination findings, which will be supported by structured data. Details of specific structured findings can be included using CLUSTER archetypes in the 'Examination Detail' slot. This data element may be used to capture legacy data that is not available in a structured format.">
				>
				["at0005"] = <
					text = <"Examination detail">
					description = <"Structured details of the physical examination.">
					comment = <"Detailed clinical findings will be recorded as CLUSTER archetypes which can be nested within this SLOT to represent the fractal nature of clinical examination and allow for variation in clinician's requirements for examination documentation. For example: CLUSTER.palpation_of_uterus, CLUSTER.palpation_of_cervix and CLUSTER.palpation_of_fetus can be nested in this slot or within each other to represent the specific  requirements of clinicians working in the obstetric domains - content, priorities and level of detail will differ depending on clinical context, professional background and specialty.">
				>
				["at0006"] = <
					text = <"Interpretation">
					description = <"Single word, phrase or brief description which represents the clinical meaning and significance of the physical examination findings.">
					comment = <"Coding with a terminology is preferred, if possible. For example, 'normal examination' or 'tympanic membrane perforation'. ">
				>
				["at0007"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"Confounding factors">
					description = <"Description of any incidental factors that may have contributed to the physical examination findings.">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Device details">
					description = <"Details about any device used during the physical examination.">
				>
				["at0011"] = <
					text = <"Comment">
					description = <"Additional narrative about the overall physical examination findings not captured in other fields.">
				>
				["at0012"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"e.g. Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
			>
		>
		["es"] = <
			items = <
				["at0000"] = <
					text = <"Hallazgos de la examinación física">
					description = <"Hallazgos observados durante la examinación física del paciente">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"*Any event(en)">
					description = <"*Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.(en)">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Descripción">
					description = <"Descripción narrativa del total de los hallazgos durante la examinación física del paciente">
				>
				["at0005"] = <
					text = <"Detalles de la examinación">
					description = <"Detalles estructurados de la examinación física">
				>
				["at0006"] = <
					text = <"Interpretación">
					description = <"Descripción corta que representa el significado clínico de los hallazgos">
				>
				["at0007"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"Factores de confusión">
					description = <"Descripción de los factores que pueden haber obstruido o dificultado la examinación física y los hallazgos">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Detalles del dispositivo">
					description = <"Detalles sobre cualquier dispositivo utilizado durante la examinación física">
				>
				["at0011"] = <
					text = <"Comentario">
					description = <"Información narrativa adicional sobre los hallazgos durante la examinación física">
				>
				["at0012"] = <
					text = <"Extensión">
					description = <"Información adicional requerida para registrar contenido local o alinear con otros modelos o formalismos">
				>
			>
		>
	>
